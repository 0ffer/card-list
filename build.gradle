buildscript {
	dependencies {
		classpath "io.crnk:crnk-gen-gradle:$CRNK_VERSION"
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '2.1.5.RELEASE'
	id "org.liquibase.gradle" version "2.0.1"
	id 'org.asciidoctor.convert' version "2.2.0"
}


group = 'ru.offer'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '12'

configurations {
	crnkJavaDocToXmlAdditional
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	jcenter()
}

dependencies {
	implementation platform("io.crnk:crnk-bom:$CRNK_VERSION")
	implementation platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")

	implementation "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "org.springframework.boot:spring-boot-starter-web"

	implementation "io.crnk:crnk-setup-spring-boot2"
	implementation "io.crnk:crnk-data-jpa"
	implementation "io.crnk:crnk-home"

	implementation "org.liquibase:liquibase-core"

	runtimeOnly "org.postgresql:postgresql:$postgresqlDriverVersion"

	compileOnly "org.projectlombok:lombok:$lombokVerion"
	annotationProcessor "org.projectlombok:lombok:$lombokVerion"

	// Actication of plain JSON format (simplify JSON API data fromat)
	//implementation "io.crnk:crnk-format-plain-json"

	testCompileOnly "org.projectlombok:lombok:$lombokVerion"
	testAnnotationProcessor "org.projectlombok:lombok:$lombokVerion"
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "io.crnk:crnk-client"
	testImplementation "io.crnk:crnk-gen-asciidoc"
	testImplementation "org.apache.httpcomponents:httpclient:$apacheHttpClientVerison"
	testImplementation "org.testcontainers:postgresql:$testContainersPostgresqlVersion"

	// Deps for documentation generation.
	crnkJavaDocToXmlAdditional "javax.xml.bind:jaxb-api:2.2.11"
	crnkJavaDocToXmlAdditional "com.sun.xml.bind:jaxb-core:2.2.11"
	crnkJavaDocToXmlAdditional "com.sun.xml.bind:jaxb-impl:2.2.11"
	crnkJavaDocToXmlAdditional "javax.activation:activation:1.1.1"

	liquibaseRuntime "ch.qos.logback:logback-classic:1.2.3"
	liquibaseRuntime "ch.qos.logback:logback-core:1.2.3"
	liquibaseRuntime "org.slf4j:slf4j-api:1.7.26"
	liquibaseRuntime "org.liquibase:liquibase-core:3.6.3"
	liquibaseRuntime "org.postgresql:postgresql:42.2.5"
}

apply plugin: 'crnk-gen'
crnkGen {
	runtime {
		configuration = 'test'
	}

	forked = false
	resourcePackages = ['ru.offer.cards.model']

	asciidoc {
		enabled = true
	}
}
crnkGen.init()

/* region documentation */

crnkJavadocToXml {
	options.getDocletpath().addAll(project.configurations.crnkJavaDocToXmlAdditional.getFiles())
}

asciidoctor {
	sourceDir = file('build/generated/source/asciidoc')
	backends = ['html5']
	attributes 'source-highlighter': 'coderay'
	sources {
		include 'index.adoc'
	}
}

tasks.test.doFirst {
	delete 'build/generated/source/asciidoc'
}
tasks.check.dependsOn asciidoctor
tasks.generateAsciidoc.dependsOn test
tasks.asciidoctor.dependsOn generateAsciidoc
tasks.build.dependsOn asciidoctor

/* endregion documentation */

liquibase {
	activities {
		main {
			changeLogFile 'main.xml'
			url 'jdbc:postgresql://localhost:5433/postgres'
			username 'postgres'
			password 'postgres'
		}
	}
}
